plugins {
    id("dev.kikugie.stonecutter") version "0.5.1"
}
apply from: rootProject.file('buildscript/standalone.gradle')
apply plugin: 'fabric-loom'

configurations.configureEach {
    resolutionStrategy {
        force "org.slf4j:slf4j-api:1.7.36" // Introduced by Minecraft itself
    }
}

java {
    def javaVersion = project.property("deps.java") == 17 ? JavaVersion.VERSION_17 : project.property("deps.java") == 21 ? JavaVersion.VERSION_21 : null;
    targetCompatibility = javaVersion
    sourceCompatibility = javaVersion
}

processResources {
    def version = project.version
    def loaderVersion = project.property("deps.fabric_loader")
    def minecraftVersion = ">=${project.property('mod.min_target')}- <=${project.property('mod.max_target')}"

    inputs.property("version", version)
    inputs.property("loaderVersion", loaderVersion)
    inputs.property("minecraftVersion", minecraftVersion)

    filesMatching('fabric.mod.json') {
        expand(VERSION: version, LOADER_VERSION: loaderVersion, MINECRAFT_VERSION: minecraftVersion)
    }
    dependsOn generateRuntimeDownloadResourceForRuntimeDownloadOnly
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()
}

stonecutter {
    def adventure = project.property("deps.adventure_platform_fabric")
    dependency("adventure", adventure)
    dependency("java", project.property("deps.java"))
}

tasks.register('copyRemappedJar', Copy) {
    from remapJar.archiveFile
    into rootProject.file('jars/fabric')
}

remapJar {
    dependsOn shadowJar
    mustRunAfter shadowJar
    inputFile = shadowJar.archiveFile
    if (project.property('mod.min_target') == project.property('mod.max_target')) {
        archiveBaseName = "DiscordSRV-Fabric-${project.property('mod.min_target')}"
    } else {
        archiveBaseName = "DiscordSRV-Fabric-${project.property('mod.min_target')}-${project.property('mod.max_target')}"
    }
    archiveClassifier = jar.archiveClassifier

    finalizedBy copyRemappedJar
}

artifacts {
    archives remapJar
    shadow shadowJar
}

loom {
    serverOnlyMinecraftJar()
}

repositories {
    exclusiveContent {
        forRepository {
            maven { url = 'https://maven.fabricmc.net/' }
        }
        filter {
            includeGroup 'net.fabricmc'
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${stonecutter.current.project}")
    mappings("net.fabricmc:yarn:${property("deps.yarn_mappings")}:v2")
    compileOnly("net.fabricmc:fabric-loader:${property("deps.fabric_loader")}")

    // Fabric API
    Set<String> apiModules = [
        'fabric-api-base',
        'fabric-lifecycle-events-v1',
        'fabric-entity-events-v1',
        'fabric-networking-api-v1',
        'fabric-message-api-v1',
    ]
    apiModules.forEach {
        modImplementation(fabricApi.module(it, property("deps.fabric_api")))
    }

    modImplementation(libs.fabric.permissions.api)
    include(libs.fabric.permissions.api)

    // API
    annotationProcessor project(':api')
    shadow project(':common:common-api')

    // Common
    shadow project(':common')

    // Adventure
    modImplementation("net.kyori:adventure-platform-fabric:${property("deps.adventure_platform_fabric")}")
    include("net.kyori:adventure-platform-fabric:${property("deps.adventure_platform_fabric")}")

    // DependencyDownload
    shadow(libs.mcdependencydownload.fabric) {
        exclude module: 'fabric-loader'
    }
}