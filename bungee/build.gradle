// TODO: not yet functional, so jar artifact disabled
allprojects {
    tasks.withType(Jar).configureEach {
        enabled = false
    }
}

shadowJar {
    archiveFileName = 'bungee.jarinjar'

    configure {
        [
                'net.kyori',
                'net.kyori.adventure.platform',
                'net.kyori.adventure.text.serializer'
        ].each {
            relocate it, 'com.discordsrv.dependencies.' + it
        }
    }
    // More relocations in buildscript/relocations.gradle
}

apply from: rootProject.file('buildscript/runtime.gradle')

allprojects {
    repositories {
        exclusiveContent {
            forRepository {
                maven { url = 'https://repo.papermc.io/repository/maven-public/' }
            }
            filter {
                includeGroup 'net.md-5'
            }
        }
    }

    dependencies {
        // Platform
        compileOnly(libs.bungee)
    }
}

dependencies {
    // API
    annotationProcessor project(':api')

    // Common
    implementation project(':common')

    // DependencyDownload
    implementation(libs.mcdependencydownload.bungee.bootstrap)

    // Adventure
    runtimeDownloadApi(libs.adventure.platform.bungee)
    runtimeDownloadApi(libs.adventure.serializer.bungee)
}
