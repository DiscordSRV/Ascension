apply from: rootProject.file('buildscript/loader.gradle')

dependencies {
    // API
    implementation project(':common:common-api')


    // DependencyDownload
    implementation(libs.mcdependencydownload.neoforge.loader)
}

base {
    archivesName = "DiscordSRV-NeoForge-${stonecutter.current.project}"

    if (stonecutter.current.version == "1.21.6") {
        archivesName = "DiscordSRV-NeoForge-${stonecutter.current.project}-1.21.8"
    } else if (stonecutter.current.version == "1.21.9") {
        archivesName = "DiscordSRV-NeoForge-${stonecutter.current.project}-1.21.10"
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : stonecutter.current.project,
            minecraft_version_range: project.property("minecraft.version_range"),
            neo_version            : project.property("deps.neoforge_version"),
            mod_id                 : "discordsrv",
            mod_name               : "DiscordSRV",
            mod_license            : "GPLv3",
            mod_version            : version,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }

    dependsOn generateRuntimeDownloadResourceForRuntimeDownloadOnly
}