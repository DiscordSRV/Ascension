apply from: rootProject.file('buildscript/relocations.gradle')
apply from: rootProject.file('buildscript/final.gradle')

var minecraftVersion = project.name.split("-")[1]
var stonecutterNode = rootProject.findProject("modded").subprojects.find { it.name == "${minecraftVersion}-neoforge" }

configurations.configureEach {
    resolutionStrategy {
        force "org.slf4j:slf4j-api:1.7.36"

        force "dev.vankka:dependencydownload-runtime:2.0.0"
        force "dev.vankka:dependencydownload-jarinjar-bootstrap:2.0.0"
        force "dev.vankka:dependencydownload-jarinjar-loader:2.0.0"
        force "dev.vankka:dependencydownload-jarinjar-common:2.0.0"
    }
}
architectury {
    common("neoforge")
}

java {
    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    main {
        java {
            srcDirs("../src/main/java")
        }
        resources {
            srcDirs("../src/main/resources")
        }
    }
}

spotless {
    java {
        target("src/main/java")
    }
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = "https://maven.parchmentmc.org" }
}

loom {
    silentMojangMappingsLicense()
}

processResources {
    var replaceProperties = [
            mod_id:       "discordsrv",
            mod_name:     "DiscordSRV",
            mod_license:  "GPLv3",
            mod_version:  version,

            minecraft_version: minecraftVersion,
            minecraft_version_range: stonecutterNode.property("minecraft.version_range"),

            neo_version: stonecutterNode.property("deps.neoforge_version")
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }

    dependsOn generateRuntimeDownloadResourceForRuntimeDownloadOnly
}

tasks.register('copyNeoForge', Copy) {
    from this.transformProductionNeoForge
    into rootProject.file("jars/neoforge")
}

shadowJar {
    dependsOn stonecutterNode.transformProductionNeoForge

    configurations = [project.configurations.shadow]
    mergeServiceFiles()

    relocate('com.discordsrv.unrelocate.', '')

    from stonecutterNode.tasks.shadowJar.archiveFile
}

remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile
}

transformProductionNeoForge {
    dependsOn remapJar
    input = remapJar.archiveFile

    archiveBaseName = "DiscordSRV-NeoForge-${stonecutterNode.property('minecraft.version_range_label')}"
    archiveClassifier = parent.jar.archiveClassifier

    finalizedBy copyNeoForge
}

artifacts {
    archives remapJar
    shadow shadowJar
}

dependencies {
    minecraft("com.mojang:minecraft:${minecraftVersion}")
    mappings(loom.officialMojangMappings())
    neoForge("net.neoforged:neoforge:${stonecutterNode.property("deps.neoforge_version")}")

    // DependencyDownload
    shadow(libs.mcdependencydownload.neoforge.loader)
    shadow(libs.mcdependencydownload.neoforge.bootstrap)

    // Mixin Extras
    include(modImplementation("io.github.llamalad7:mixinextras-neoforge:0.5.2"))

    // Adventure
    include(libs.adventure.api)
    include(libs.kyori.ansi)

    include(modImplementation("net.kyori:adventure-platform-neoforge:${stonecutterNode.property("deps.adventure_platform")}"){
        exclude group: 'net.kyori', module: 'ansi'
        exclude group: 'net.kyori', module: 'adventure-api'
    })
}


