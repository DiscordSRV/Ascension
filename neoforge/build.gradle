plugins {
    id("dev.kikugie.stonecutter") version "0.8.2"
    alias(libs.plugins.neogradle)
}

configurations.configureEach {
    resolutionStrategy {
        force "org.slf4j:slf4j-api:1.7.36" // Introduced by Minecraft itself
    }
}

shadowJar {
    archiveFileName = 'neoforge.jarinjar'
}
apply from: rootProject.file('buildscript/runtime.gradle')

repositories {
    mavenLocal()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }

    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

if(project.path.startsWith(":neoforge:")) {
    var deleteMappedSrc = tasks.register('deleteMappedSrc', Delete) {
        delete project.file("src")
    }

    clean.dependsOn deleteMappedSrc
}

sourceSets {
    main {
        java {
            srcDirs("versions/${stonecutter.current.project}/src/main/java")
        }
        resources {
            srcDirs("versions/${stonecutter.current.project}/src/main/resources")
        }
    }
}

spotless {
    java {
        if (stonecutter.current.isActive)
            target("src/main/java/")
    }
}

subsystems {
    parchment {
        minecraftVersion = project.property("neogradle.subsystems.parchment.minecraftVersion")
        mappingsVersion = project.property("neogradle.subsystems.parchment.mappingsVersion")
    }
}

dependencies {
    implementation("net.neoforged:neoforge:${project.property("deps.neoforge_version")}")

    // API
    annotationProcessor project(':api')
    shadow project(':common:common-api')

    // Common
    shadow project(':common')
    implementation project(':modded')

    // Adventure
    implementation("net.kyori:adventure-platform-neoforge:${project.property("deps.adventure_platform_neoforge")}") {
        exclude group: 'net.kyori', module: 'ansi'
    }
    implementation(libs.kyori.ansi)

    // DependencyDownload
    shadow(libs.mcdependencydownload.neoforge.bootstrap)
}
