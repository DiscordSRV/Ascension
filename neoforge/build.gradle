plugins {
    id("dev.kikugie.stonecutter") version "0.8.2"
    alias(libs.plugins.arch.loom)
    alias(libs.plugins.arch.plugin)
}
apply from: rootProject.file('buildscript/standalone.gradle')

configurations.configureEach {
    resolutionStrategy {
        force "org.slf4j:slf4j-api:1.7.36" // Introduced by Minecraft itself
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }

    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    main {
        java {
            srcDirs("versions/${stonecutter.current.project}/src/main/java")
        }
        resources {
            srcDirs("versions/${stonecutter.current.project}/src/main/resources", "src/generated/resources")
        }
    }
}

processResources {
    var replaceProperties = [
            minecraft_version      : stonecutter.current.project,
            minecraft_version_range: project.property("minecraft.version_range"),
            neo_version            : project.property("deps.neoforge_version"),
            mod_id                 : "discordsrv",
            mod_name               : "DiscordSRV",
            mod_license            : "GPLv3",
            mod_version            : version,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }

    dependsOn generateRuntimeDownloadResourceForRuntimeDownloadOnly
}

stonecutter {
    replacements.string(sc.current.parsed.matches(">=1.21.11")) {
        replace("ResourceLocation", "Identifier")
        replace(".location()", ".identifier()")
        replace("ResourceKey::location", "ResourceKey::identifier")
    }
}

tasks.register('copyRemappedJar', Copy) {
    from this.shadowJar
    into rootProject.file('jars/neoforge')
}

shadowJar {
    dependsOn processResources

    configurations = [project.configurations.shadow]
    mergeServiceFiles()

    archiveBaseName = "DiscordSRV-Neoforge-${sc.current.version}"
    if (sc.current.version == "1.21.6") {
        archiveBaseName = "DiscordSRV-Neoforge-${sc.current.version}-1.21.8"
    } else if (sc.current.version == "1.21.9") {
        archiveBaseName = "DiscordSRV-Neoforge-${sc.current.version}-1.21.10"
    }
    archiveClassifier = jar.archiveClassifier

    finalizedBy copyRemappedJar
}

spotless {
    java {
        if (stonecutter.current.isActive)
            target("src/main/java/")
    }
}

stonecutter {
    replacements.string(sc.current.parsed.matches(">=1.21.11")) {
        replace("ResourceLocation", "Identifier")
        replace(".location()", ".identifier()")
        replace("ResourceKey::location", "ResourceKey::identifier")
    }
}

loom {
    serverOnlyMinecraftJar()
    runConfigs.all {
        ideConfigGenerated(true) // Run configurations are not created for subprojects by default
        runDir = "../../run" // Use a shared run folder and create separate worlds
    }
    silentMojangMappingsLicense()
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = "https://maven.parchmentmc.org" }
}

dependencies {
    neoForge("net.neoforged:neoforge:${project.property("deps.neoforge_version")}")
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${deps.parchment_minecraft_version}:${deps.parchment_mappings_version}")
    }

    implementation("net.neoforged.fancymodloader:loader:${project.property("deps.neoforge_fml_version")}")

    // API
    annotationProcessor project(':api')
    shadow project(':common:common-api')

    // Common
    shadow project(':common')
    implementation project(':modded')

    // Adventure
    modImplementation("net.kyori:adventure-platform-neoforge:${project.property("deps.adventure_platform_neoforge")}") {
        exclude group: 'net.kyori', module: 'ansi'
    }
    implementation(libs.kyori.ansi)

    // DependencyDownload
    shadow(libs.mcdependencydownload.neoforge.loader)
    shadow(libs.mcdependencydownload.neoforge.bootstrap)
}
