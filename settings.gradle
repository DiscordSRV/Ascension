pluginManagement {
    repositories {
        mavenLocal()
        maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url = 'https://maven.fabricmc.net/'}
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // Buildscript
            plugin('shadow', 'io.github.goooler.shadow').version('8.1.8')
            plugin('spotless', 'com.diffplug.spotless').version('6.25.0')
            plugin('blossom', 'net.kyori.blossom').version('2.1.0')
            plugin('indra-git', 'net.kyori.indra.git').version('3.1.3')
            plugin('indra-licenser', 'net.kyori.indra.licenser.spotless').version('3.1.3')
            plugin('idea-ext', 'org.jetbrains.gradle.plugin.idea-ext').version('1.1.7')

            // Run platforms
            version('runtask', '2.3.1')
            plugin('run-paper', 'xyz.jpenilla.run-paper').versionRef('runtask')
            plugin('run-velocity', 'xyz.jpenilla.run-velocity').versionRef('runtask')
            plugin('run-waterfall', 'xyz.jpenilla.run-waterfall').versionRef('runtask')

            // Bukkit
            library('bukkit-minimum', 'org.bukkit', 'bukkit').version('1.8.8-R0.1-SNAPSHOT')
            library('spigotapi-onetwelve', 'org.spigotmc', 'spigot-api').version('1.12.2-R0.1-SNAPSHOT')
            library('spigotapi-latest', 'org.spigotmc', 'spigot-api').version('1.21.2-R0.1-SNAPSHOT')

            // Paper
            library('paperapi-minimum', 'com.destroystokyo.paper', 'paper-api').version('1.16.5-R0.1-SNAPSHOT')
            library('paperapi-latest', 'io.papermc.paper', 'paper-api').version('1.21.4-R0.1-SNAPSHOT')
            library('folia', 'dev.folia', 'folia-api').version('1.21.4-R0.1-SNAPSHOT')

            // Bungee
            library('bungee', 'net.md-5', 'bungeecord-api').version('1.21-R0.1-SNAPSHOT')

            // Velocity
            library('velocity', 'com.velocitypowered', 'velocity-api').version('3.3.0-SNAPSHOT')

            // Fabric
            version('fabric-loom', '1.9-SNAPSHOT')
            plugin('fabric-loom', 'fabric-loom').versionRef('fabric-loom')
            library('fabric-minecraft', 'com.mojang', 'minecraft').version('1.21.4')
            library('fabric-yarn', 'net.fabricmc', 'yarn').version('1.21.4+build.8')
            library('fabric-loader', 'net.fabricmc', 'fabric-loader').version('0.16.9')
            library('fabric-api', 'net.fabricmc.fabric-api', 'fabric-api').version('0.114.2+1.21.4')
            library('fabric-permissions-api', 'me.lucko', 'fabric-permissions-api').version('0.3.3')

            // DependencyDownload
            version('dependencydownload', '2.0.0-SNAPSHOT')
            plugin('dependencydownload-plugin', 'dev.vankka.dependencydownload.plugin').versionRef('dependencydownload')
            library('dependencydownload-runtime', 'dev.vankka', 'dependencydownload-runtime').versionRef('dependencydownload')
            library('dependencydownload-jarinjar-bootstrap', 'dev.vankka', 'dependencydownload-jarinjar-bootstrap').versionRef('dependencydownload')
            library('dependencydownload-jarinjar-loader', 'dev.vankka', 'dependencydownload-jarinjar-loader').versionRef('dependencydownload')

            // MinecraftDependencyDownload
            version('mcdependencydownload', '1.0.0')
            library('mcdependencydownload-bukkit-bootstrap', 'dev.vankka', 'minecraftdependencydownload-bukkit').versionRef('mcdependencydownload')
            library('mcdependencydownload-bukkit-loader', 'dev.vankka', 'minecraftdependencydownload-bukkit-loader').versionRef('mcdependencydownload')
            library('mcdependencydownload-bungee-bootstrap', 'dev.vankka', 'minecraftdependencydownload-bungee').versionRef('mcdependencydownload')
            library('mcdependencydownload-bungee-loader', 'dev.vankka', 'minecraftdependencydownload-bungee-loader').versionRef('mcdependencydownload')
            library('mcdependencydownload-velocity', 'dev.vankka', 'minecraftdependencydownload-velocity').versionRef('mcdependencydownload')
            library('mcdependencydownload-fabric', 'dev.vankka', 'minecraftdependencydownload-fabric').versionRef('mcdependencydownload')

            // Annotations
            library('jetbrains-annotations', 'org.jetbrains', 'annotations').version('24.1.0')

            // JDA
            library('jda', 'net.dv8tion', 'JDA').version('5.2.1')

            // OkHTTP
            library('okhttp', 'com.squareup.okhttp3', 'okhttp').version('4.12.0')

            // Apache commons
            library('commons-lang', 'org.apache.commons', 'commons-lang3').version('3.14.0')
            library('commons-io', 'commons-io', 'commons-io').version('2.15.1')

            // Caffeine
            library('caffeine', 'com.github.ben-manes.caffeine', 'caffeine').version {
                prefer '2.9.3'
                reject '[3,)' // Java 11
            }

            // DynamicProxy
            version('dynamicproxy', '1.0.1-SNAPSHOT')
            library('dynamicproxy-api', 'dev.vankka', 'dynamicproxy').versionRef('dynamicproxy')
            library('dynamicproxy-runtime', 'dev.vankka', 'dynamicproxy-runtime').versionRef('dynamicproxy')

            // Jackson
            library('jackson-databind', 'com.fasterxml.jackson.core', 'jackson-databind').version('2.16.1')

            // Config(urate)
            version('configurate', '4.2.0-yamlbranch-5')
            library('configurate-yaml', 'org.spongepowered', 'configurate-yaml').versionRef('configurate')
            library('configurate-jackson', 'org.spongepowered', 'configurate-jackson').versionRef('configurate')

            // DB
            library('hikaricp', 'com.zaxxer', 'HikariCP').version {
                prefer '4.0.3'
                reject '[5,)' // Java 11
            }
            library('h2', 'com.h2database', 'h2').version('2.1.210')
            library('mysql', 'mysql', 'mysql-connector-java').version('8.0.28')
            library('mariadb', 'org.mariadb.jdbc', 'mariadb-java-client').version('3.1.4')

            // MinecraftAuth lib
            library('minecraftauth-lib', 'me.minecraftauth', 'lib').version('1.1.0')

            // Brigadier & Commodore
            library('brigadier', 'com.mojang', 'brigadier').version('1.0.18')
            library('commodore', 'me.lucko', 'commodore').version('2.2')

            // Integrations
            library('luckperms', 'net.luckperms', 'api').version('5.4')
            library('vaultapi', 'net.milkbowl.vault', 'VaultAPI').version('1.7')
            library('placeholderapi-bukkit', 'me.clip', 'placeholderapi').version('2.11.1')
            library('townychat', 'com.palmergames.bukkit', 'TownyChat').version('0.45')
            library('venturechat', 'mineverse.aust1n46', 'venturechat').version('3.5.0')
            library('chatty', 'ru.mrbrikster', 'chatty-api').version('2.19.13')
            library('lunachat', 'com.github.ucchyocean.lc', 'LunaChat').version('3.0.16')
            library('bungeecord-chat', 'net.md-5', 'bungeecord-chat').version('1.12-SNAPSHOT')
            library('mcmmo', 'com.gmail.nossr50', 'mcmmo').version('2.1.220')
            library('griefprevention', 'me.ryanhamshire', 'GriefPrevention').version('16.18.1')
            library('essentialsx', 'net.essentialsx', 'EssentialsX').version('2.20.1')

            // Logging
            library('slf4j-api', 'org.slf4j', 'slf4j-api').version {
                prefer '1.7.36'
                reject '[2,)' // Uses ServiceLoader
            }
            library('log4j-core', 'org.apache.logging.log4j', 'log4j-core').version('2.0-beta9')

            // Adventure
            version('adventure', '4.15.0')
            library('adventure-api', 'net.kyori', 'adventure-api').versionRef('adventure')
            library('adventure-serializer-plain', 'net.kyori', 'adventure-text-serializer-plain').versionRef('adventure')
            library('adventure-serializer-legacy', 'net.kyori', 'adventure-text-serializer-legacy').versionRef('adventure')
            library('adventure-serializer-gson', 'net.kyori', 'adventure-text-serializer-gson').versionRef('adventure')
            library('adventure-serializer-ansi', 'net.kyori', 'adventure-text-serializer-ansi').versionRef('adventure')

            // Adventure Platform
            version('adventure-platform', '4.3.4')
            version('adventure-platform-mod', '6.2.0')
            library('adventure-platform-bukkit', 'net.kyori', 'adventure-platform-bukkit').versionRef('adventure-platform')
            library('adventure-platform-bungee', 'net.kyori', 'adventure-platform-bungeecord').versionRef('adventure-platform')
            library('adventure-platform-fabric', 'net.kyori', 'adventure-platform-fabric').versionRef('adventure-platform-mod')
            library('adventure-serializer-bungee', 'net.kyori', 'adventure-text-serializer-bungeecord').versionRef('adventure-platform')

            // Upgrade ansi (used by ansi serializer)
            library('kyori-ansi', 'net.kyori', 'ansi').version('1.1.0')

            // MCDiscordReserializer & EnhancedLegacyText
            library('mcdiscordreserializer', 'dev.vankka', 'mcdiscordreserializer').version('4.4.0-SNAPSHOT')
            library('enhancedlegacytext', 'dev.vankka', 'enhancedlegacytext').version('2.0.0-SNAPSHOT')

            // JUnit
            library('junit-jupiter', 'org.junit.jupiter', 'junit-jupiter').version('5.10.2')
        }
    }
}

rootProject.name = 'DiscordSRV-Ascension'

[
        'common', 'common:api', 'common:unrelocate',
        'i18n',
        'api',
        // Bukkit
        'bukkit', 'bukkit:shared', 'bukkit:loader',
        'bukkit:compat:bukkit1_12', 'bukkit:compat:spigot', 'bukkit:compat:paper', 'bukkit:compat:folia',
        // Bungee
        'bungee', 'bungee:loader',
        // Velocity
        'velocity',
        // Fabric
        'fabric'
].each {
    include it
    findProject(':' + it).name = String.join('-', it.split(':'))
}
