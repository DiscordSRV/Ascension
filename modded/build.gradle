plugins {
    id("dev.kikugie.stonecutter") version "0.8.3"
    alias(libs.plugins.arch.loom)
    alias(libs.plugins.arch.plugin)
}
apply from: rootProject.file('buildscript/standalone.gradle')

configurations.configureEach {
    resolutionStrategy {
        force "org.slf4j:slf4j-api:1.7.36" // Introduced by Minecraft itself
        force "net.fabricmc:fabric-loader:0.14.0"
    }
}

var loader = project.property("loom.platform")
var minecraftVersion = sc.current.version.split("-")[0]

architectury {
    common("fabric", "neoforge")
}

java {
    var java = sc.eval(minecraftVersion, ">=1.20.5") ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17

    targetCompatibility = java
    sourceCompatibility = java
}

sourceSets {
    main {
        java {
            srcDirs("versions/${sc.current.version}/src/main/java")
        }
        resources {
            srcDirs("versions/${sc.current.version}/src/main/resources")
        }
    }
}

stonecutter {
    var is1_21_11 = sc.eval(minecraftVersion, ">=1.21.11")
    if (loader == "fabric") {
        replacements.string(is1_21_11) {
            replace("ResourceLocation", "Identifier")
            replace(".location()", ".identifier()")
            replace("ResourceKey::location", "ResourceKey::identifier")
        }
    } else if (loader == "neoforge") {
        replacements.string(!is1_21_11) {
            replace("Identifier", "ResourceLocation")
            replace(".identifier()", ".location()")
            replace("ResourceKey::identifier", "ResourceKey::location")
        }
    }
}

processResources {
    var replaceProperties = [
            mod_id:       "discordsrv",
            mod_name:     "DiscordSRV",
            mod_license:  "GPLv3",
            mod_version:  version,

            minecraft_version: minecraftVersion,
            minecraft_version_range: project.property("minecraft.version_range"),

            fabric_loader_version: project.property("deps.fabric_loader"),
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json']) {
        expand replaceProperties
    }

    dependsOn generateRuntimeDownloadResourceForRuntimeDownloadOnly
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()

    if (loader == "neoforge") archiveFileName = "neoforge.jarinjar"

    relocate('com.discordsrv.unrelocate.', '')
}

tasks.register('copyRemappedJar', Copy) {
    from this.transformProductionNeoForge
    into rootProject.file("jars/${loader}")
}

remapJar {
    dependsOn processResources
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile
}

transformProductionFabric {
    if (loader == "neoforge") {
        enabled = false
        return
    }

    dependsOn remapJar
    input = remapJar.archiveFile

    archiveBaseName = "DiscordSRV-${loader.capitalize()}-${project.property("minecraft.version_range_label")}"
    archiveClassifier = project.parent.jar.archiveClassifier

    finalizedBy copyRemappedJar
}

transformProductionNeoForge {
    if (loader == "fabric") {
        enabled = false
        return
    }

    dependsOn remapJar
    input = remapJar.archiveFile

    archiveFileName = "neoforge.jarinjar"
}

spotless {
    java {
        if (sc.current.isActive)
            target("src/main/java/")
    }
}

artifacts {
    archives remapJar
    shadow shadowJar
}

loom {
//    serverOnlyMinecraftJar() // Broken in neoforge 21.10.57-beta and above - see https://github.com/architectury/architectury-loom/issues/312
    silentMojangMappingsLicense()
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = "https://maven.parchmentmc.org" }
    exclusiveContent {
        forRepository {
            maven { url = 'https://maven.fabricmc.net/' }
        }
        filter {
            includeGroup 'net.fabricmc'
        }
    }
    exclusiveContent {
        forRepository {
            maven { url = 'https://maven.nucleoid.xyz/' }
        }
        filter {
            includeGroup 'eu.pb4'
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${minecraftVersion}")
    mappings(loom.officialMojangMappings())

    // Mixin Extras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.2"))

    // API
    annotationProcessor project(':api')
    shadow project(':common:common-api')

    // Common
    shadow project(':common')

    implementation(libs.adventure.api)
    implementation(libs.kyori.ansi)

    if (loader == "fabric") {
        include(modImplementation("net.kyori:adventure-platform-fabric:${project.property("deps.adventure_platform")}"){
            exclude group: 'net.kyori', module: 'ansi'
            exclude group: 'net.kyori', module: 'adventure-api'
        })

        include(libs.adventure.api)
        include(libs.kyori.ansi)

        compileOnly("net.fabricmc:fabric-loader:${project.property("deps.fabric_loader")}")
        include(implementation("io.github.llamalad7:mixinextras-fabric:0.5.2"))

        // Fabric API
        Set<String> apiModules = [
                'fabric-api-base',
                'fabric-lifecycle-events-v1',
                'fabric-entity-events-v1',
                'fabric-networking-api-v1',
                'fabric-message-api-v1',
                sc.eval(minecraftVersion, "<1.21.9") ? 'fabric-resource-loader-v0' : 'fabric-resource-loader-v1'
        ]
        apiModules.forEach {
            modImplementation(fabricApi.module(it, project.property("deps.fabric_api")))
        }

        // Minecraft versions under 1.21.6 should use 0.3.3, while 1.21.6 and above should use 0.4.1
        modImplementation("me.lucko:fabric-permissions-api:${sc.eval(minecraftVersion, ">= 1.21.6") ? '0.4.1' : '0.3.3'}") {
            exclude group: 'net.fabricmc.fabric-api', module: 'fabric-api-base'
            exclude group: 'net.fabricmc.fabric-api', module: 'fabric-api-bom'
        }
        include ("me.lucko:fabric-permissions-api:${sc.eval(minecraftVersion, ">= 1.21.6") ? '0.4.1' : '0.3.3'}")

        // Integrations
        modCompileOnly("eu.pb4:placeholder-api:${project.property("deps.text_placeholder_api")}")

        // DependencyDownload
        shadow(libs.mcdependencydownload.fabric) {
            exclude module: 'fabric-loader'
        }
    } else if (loader == "neoforge") {
        modCompileOnly("net.kyori:adventure-platform-neoforge:${project.property("deps.adventure_platform")}"){
            exclude group: 'net.kyori', module: 'ansi'
            exclude group: 'net.kyori', module: 'adventure-api'
        }

        neoForge("net.neoforged:neoforge:${project.property("deps.neoforge_version")}")

        implementation("net.neoforged.fancymodloader:loader:${project.property("deps.neoforge_fml_version")}")

        // DependencyDownload
        compileOnly(libs.mcdependencydownload.neoforge.loader)
        shadow(libs.mcdependencydownload.neoforge.bootstrap)
    }
}
