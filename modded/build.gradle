plugins {
    id("dev.kikugie.stonecutter") version "0.8.2"
    alias(libs.plugins.arch.loom)
    alias(libs.plugins.arch.plugin)
}
apply from: rootProject.file('buildscript/standalone.gradle')

configurations.configureEach {
    resolutionStrategy {
        force "org.slf4j:slf4j-api:1.7.36" // Introduced by Minecraft itself
    }
}

var loader = project.property("loom.platform")

java {
    var java = sc.current.parsed.matches(">=1.20.5") ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17

    targetCompatibility = java
    sourceCompatibility = java
}

sourceSets {
    main {
        java {
            srcDirs("versions/${sc.current.version}/src/main/java")
        }
        resources {
            srcDirs("versions/${sc.current.version}/src/main/resources")
        }
    }
}

processResources {
    var replaceProperties = [
            mod_id:       "discordsrv",
            mod_name:     "DiscordSRV",
            mod_license:  "GPLv3",
            mod_version:  version,

            minecraft_version: sc.current.version,
            minecraft_version_range: project.property("minecraft.version_range"),

            fabric_loader_version: project.property("deps.fabric_loader"),
            neo_version: project.property("deps.neoforge_version")
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'fabric.mod.json']) {
        expand replaceProperties
    }

    dependsOn generateRuntimeDownloadResourceForRuntimeDownloadOnly
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()
}

stonecutter {
    replacements.string(sc.current.parsed.matches(">=1.21.11")) {
        replace("ResourceLocation", "Identifier")
        replace(".location()", ".identifier()")
        replace("ResourceKey::location", "ResourceKey::identifier")
    }
}

tasks.register('copyRemappedJar', Copy) {
    from this.shadowJar
    into rootProject.file("jars/${loader}")
}

remapJar {
    dependsOn processResources

//    configurations = [project.configurations.shadow]
//    mergeServiceFiles()

    archiveBaseName = "DiscordSRV-${loader}-${project.property("minecraft.version_range_label")}"
    archiveClassifier = jar.archiveClassifier

    finalizedBy copyRemappedJar
}

spotless {
    java {
        if (sc.current.isActive)
            target("src/main/java/")
    }
}

artifacts {
    archives remapJar
    shadow shadowJar
}

loom {
    serverOnlyMinecraftJar()
    runConfigs.all {
        ideConfigGenerated(true) // Run configurations are not created for subprojects by default
        runDir = "../../run" // Use a shared run folder and create separate worlds
    }
    silentMojangMappingsLicense()
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = "https://maven.parchmentmc.org" }
    exclusiveContent {
        forRepository {
            maven { url = 'https://maven.fabricmc.net/' }
        }
        filter {
            includeGroup 'net.fabricmc'
        }
    }
    exclusiveContent {
        forRepository {
            maven { url = 'https://maven.nucleoid.xyz/' }
        }
        filter {
            includeGroup 'eu.pb4'
        }
    }
}

architectury {
    common("fabric", "neoforge")
}

dependencies {
    minecraft("com.mojang:minecraft:${sc.current.version.split("-")[0]}")

    // API
    annotationProcessor project(':api')
    shadow project(':common:common-api')

    // Common
    shadow project(':common')

    var adventure = sc.current.parsed >= "1.21" ? "adventure-platform-mod-shared" : "adventure-platform-fabric"
    modImplementation("net.kyori:${adventure}:${project.property("deps.adventure_platform")}") {
        exclude group: 'net.kyori', module: 'ansi'
    }
    include("net.kyori:${adventure}:${project.property("deps.adventure_platform")}")

    implementation(libs.kyori.ansi)
    include(libs.kyori.ansi)

    if (loader == "fabric") {
        mappings(loom.officialMojangMappings())
        compileOnly("net.fabricmc:fabric-loader:${project.property("deps.fabric_loader")}")

        // Fabric API
        Set<String> apiModules = [
                'fabric-api-base',
                'fabric-lifecycle-events-v1',
                'fabric-entity-events-v1',
                'fabric-networking-api-v1',
        ]
        if (sc.current.parsed >= "1.19.2") apiModules.add('fabric-message-api-v1')
        if (sc.current.parsed < "1.21.9") apiModules.add('fabric-resource-loader-v0')
        else apiModules.add('fabric-resource-loader-v1')

        apiModules.forEach {
            modImplementation(fabricApi.module(it, project.property("deps.fabric_api")))
        }

        // Minecraft versions under 1.21.6 should use 0.3.3, while 1.21.6 and above should use 0.4.1
        modImplementation("me.lucko:fabric-permissions-api:${sc.current.parsed >= "1.21.6" ? '0.4.1' : '0.3.3'}") {
            exclude group: 'net.fabricmc.fabric-api', module: 'fabric-api-base'
            exclude group: 'net.fabricmc.fabric-api', module: 'fabric-api-bom'
        }
        include ("me.lucko:fabric-permissions-api:${sc.current.parsed >= "1.21.6" ? '0.4.1' : '0.3.3'}")

        // Integrations
        modCompileOnly("eu.pb4:placeholder-api:${project.property("deps.text_placeholder_api")}")

        // DependencyDownload
        shadow(libs.mcdependencydownload.fabric) {
            exclude module: 'fabric-loader'
        }
    } else if (loader == "neoforge") {
        neoForge("net.neoforged:neoforge:${project.property("deps.neoforge_version")}")
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${project.property("deps.parchment_minecraft_version")}:${project.property("deps.parchment_mappings_version")}")
        }

        implementation("net.neoforged.fancymodloader:loader:${project.property("deps.neoforge_fml_version")}")

        // DependencyDownload
        shadow(libs.mcdependencydownload.neoforge.loader)
        shadow(libs.mcdependencydownload.neoforge.bootstrap)
    }
}
